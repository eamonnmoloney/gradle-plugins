apply plugin: 'groovy'
apply plugin: 'idea'
apply plugin: 'maven'


ext.codenarcRuleSetFiles = [ 'codenarc.groovy' ]
ext.codenarcExtensions   = [ '**/*.groovy'     ]
apply from: 'file:src/main/groovy/CodeNarc.gradle'


defaultTasks 'clean', 'codenarc', 'groovydoc', 'build', 'install'


sourceCompatibility = 1.6
group               = 'com.github.goldin.plugins'


repositories {
    mavenRepo url: 'http://evgenyg.artifactoryonline.com/evgenyg/repo/'
}

dependencies {
    groovy      localGroovy()
    compile     gradleApi(),
                'org.gcontracts:gcontracts-core:1.2.5'
    testCompile 'junit:junit:4.10'
}


idea.project {
    jdkName = '1.6'
    ipr.withXml { provider -> provider.node.component.find { it.@name == 'VcsDirectoryMappings' }.mapping.@vcs = 'Git' }
}

idea.module {
    downloadSources = true
    downloadJavadoc = true
}

/**
 * Gradle Wrapper
 */

task wrapper( type: Wrapper ) { gradleVersion = '1.0' }


/**
 * Adding sources and groovydocs
 */

task sourcesJar( type: Jar, dependsOn: classes ) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task groovydocJar( type: Jar, dependsOn: groovydoc ) {
     classifier = 'javadoc'
     from groovydoc.destinationDir
}

artifacts {
     archives sourcesJar
     archives groovydocJar
}
