apply plugin: 'groovy'
apply plugin: 'idea'
apply plugin: 'maven'


ext.codenarcRuleSetFiles = [ 'codenarc.groovy' ]
ext.codenarcExtensions   = [ '**/*.groovy'     ]
apply from: 'file:src/main/groovy/CodeNarc.gradle'


defaultTasks 'clean', 'codenarc', 'build', 'install'


sourceCompatibility = 1.6
group               = 'com.github.goldin.plugins'


repositories { mavenRepo url: 'http://evgenyg.artifactoryonline.com/evgenyg/repo/' }
dependencies {
    groovy      localGroovy()
    compile     gradleApi()
    testCompile 'junit:junit:4.10'
}


idea.project {
    jdkName = '1.6.0_31'
    ipr.withXml { provider -> provider.node.component.find { it.@name == 'VcsDirectoryMappings' }.mapping.@vcs = 'Git' }
}

idea.module {
    downloadSources = true
    downloadJavadoc = true
}


task wrapper( type: Wrapper ) { gradleVersion = '1.0-milestone-9' }


/**
 * Adding sources
 */
task sourcesJar( type: Jar, dependsOn: classes ) {
     classifier = 'sources'
     from sourceSets.main.allSource
}
artifacts { archives sourcesJar }
