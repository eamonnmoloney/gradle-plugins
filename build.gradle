apply plugin: 'groovy'
apply plugin: 'idea'
apply plugin: 'maven'


codenarcRuleSetFiles = [ 'codenarc/codenarc.txt' ]
apply from: 'file:src/main/groovy/CodeNarc.gradle'


defaultTasks 'clean', 'build'


sourceCompatibility = 1.6
group               = 'com.goldin.plugins'
version             = '0.1-RC3'


repositories { mavenRepo urls: 'http://evgeny-goldin.org/artifactory/repo/' }
dependencies {
    groovy      localGroovy()
    compile     gradleApi()
    compile     'org.gcontracts:gcontracts-core:1.2.4'
    testCompile 'junit:junit:4.8.2'
}


idea {
    project.jdkName        = '1.6.0_26'
    module.downloadSources = true
    module.downloadJavadoc = true
}

task wrapper( type: Wrapper ) { gradleVersion = '1.0-milestone-4' }

/**
 * Adding sources artifact
 * http://gradle.1045684.n5.nabble.com/javadoc-and-source-jars-td3246213.html
 */
task sourcesJar( type: Jar, dependsOn: classes ) {
     classifier = 'sources'
     from sourceSets.main.allSource
}
artifacts { archives sourcesJar }
